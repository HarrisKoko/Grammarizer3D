global string $iterSlide;
global string $outputCountSlide;
global string $minEdgeLengthSlide;
global string $maxEdgeLengthSlide;
global string $statusLabel1;
global string $statusLabel2;
global string $statusLabel3;

global proc string readTxtFile(string $filePath) {
    int $fid = `fopen $filePath "r"`;
    string $fileContents = `fread $fid ""`;
    fclose $fid;
    return $fileContents;
}

global proc openTxtFileBrowser(string $statusLabel) {
   
    string $filePathArray[] = `fileDialog2 -fileMode 1 -caption "Select a Text File" -fileFilter "Text Files (*.txt)"`;
    if (size($filePathArray) > 0) {
        string $filePath = $filePathArray[0];
        string $fileContents = readTxtFile($filePath);
        if (size($fileContents) > 0) {
            text -e -label "Grammar successfully loaded." -backgroundColor 0 0.5 0 $statusLabel;
        } else {
            text -e -label "Failed to load grammar." -backgroundColor 0.5 0 0 $statusLabel;
        }
    }
     // call LoadGrammarCmd here based on file selected
}

global proc onCancelPressed() {
    if (`window -exists myWindow`) {
        deleteUI myWindow;
    }
}

global proc loadObject() {
    print("Calling LoadMeshCmd...\n");
    string $result;
    int $status = catch($result = `LoadMeshCmd`);

    global string $statusLabel1;
    if ($status != 0) {
        print("LoadMeshCmd failed.\n");
        text -e -label "Failed to load object." -backgroundColor 0.5 0 0 $statusLabel1;
    } else {
        print("LoadMeshCmd result: " + $result + "\n");
        text -e -label "Object loaded successfully." -backgroundColor 0 0.5 0 $statusLabel1;
    }
}

global proc exportGrammar() {
    // Placeholder
    print("ExportGrammar command here.\n");
}

global proc generateGrammar() {
    print("Calling GenerateGrammarCmd...\n");
    string $result;
    int $status = catch($result = `GenerateGrammarCmd`);

    global string $statusLabel2;
    if ($status != 0) {
        print("GenerateGrammarCmd failed.\n");
        text -e -label "Failed to generate grammar." -backgroundColor 0.5 0 0 $statusLabel2;
    } else {
        print("GenerateGrammarCmd result: " + $result + "\n");
        text -e -label "Grammar generated successfully." -backgroundColor 0 0.5 0 $statusLabel2;
    }
}

global proc generateModels(){
    print("Calling GenerateModelsCmd...\n");
    string $result;
    int $status = catch($result = `GenerateModelsCmd`);

    global string $statusLabel3;
    if ($status != 0) {
        print("GenerateModelsCmd failed.\n");
        text -e -label "Failed to generate models." -backgroundColor 0.5 0 0 $statusLabel3;
    } else {
        print("GenerateGrammarCmd result: " + $result + "\n");
        text -e -label "Models generated successfully." -backgroundColor 0 0.5 0 $statusLabel3;
    }
}

global proc showMyWindow() {
    if (`window -exists myWindow`) {
        deleteUI myWindow;
    }

    window -title "Grammarizer Tool" myWindow;
    scrollLayout;
    columnLayout -adjustableColumn true;

    // Section 1: Load from Scene
    frameLayout -label "Option 1: Use Object from Scene" -collapsable true;
    columnLayout -adjustableColumn true;
    button -label "Select Object" -command "loadObject()" -ann "Select an object in the scene to build a grammar from.";
    global string $statusLabel1;
    $statusLabel1 = `text -label "Select a mesh and press select object"
        -backgroundColor 0.5 0.5 0.5
        -height 25 -align "center"`;

    button -label "Generate Grammar" -command "generateGrammar()" -ann "Generate a grammar from the selected object.";
    global string $statusLabel2;
    $statusLabel2 = `text -label "Generate a grammar from selected mesh"
        -backgroundColor 0.5 0.5 0.5
        -height 25 -align "center"`;

    button -label "Export Grammar" -command "exportGrammar()" -ann "Export the grammar as a .json file.";
    setParent ..;
    setParent ..;

    // Section 2: Load from File
    frameLayout -label "Option 2: Load Existing Grammar File" -collapsable true;
    columnLayout -adjustableColumn true;
    global string $statusLabel3;
    $statusLabel3 = `text -label "Load an existing grammar"
        -backgroundColor 0.5 0.5 0.5
        -height 25 -align "center"`;
    button -label "Load Grammar" -command ("openTxtFileBrowser \"" + $statusLabel3 + "\"") -ann "Load a grammar text file.";
    setParent ..;
    setParent ..;

    // Generation Parameters
    frameLayout -label "Generation Settings" -collapsable true;
    columnLayout -adjustableColumn true;

    global string $iterSlide;
    $iterSlide = `intSliderGrp -label "Iterations" -field true
        -minValue 1 -maxValue 10 -value 1`;

    global string $outputCountSlide;
    $outputCountSlide = `intSliderGrp -label "Output Count" -field true
        -minValue 1 -maxValue 100 -value 10`;

    global string $minEdgeLengthSlide;
    $minEdgeLengthSlide = `floatSliderGrp -label "Min Edge Length" -field true
        -minValue 0.1 -maxValue 10.0 -value 1.0`;

    global string $maxEdgeLengthSlide;
    $maxEdgeLengthSlide = `floatSliderGrp -label "Max Edge Length" -field true
        -minValue 1.0 -maxValue 50.0 -value 10.0`;

    setParent ..;
    setParent ..;

    // Generation Button + Cancel
    rowLayout -numberOfColumns 2 -columnWidth2 150 150 -adjustableColumn 2;
    button -label "Generate Models" -command "generateModels()" -ann "Generate new models using the current grammar.";
    button -label "Cancel" -command "onCancelPressed()" -ann "Close the Grammarizer Tool window.";
    setParent ..;

    showWindow myWindow;
}

global string $gMainWindow;
if (`menu -exists myCustomMenu`) {
    deleteUI myCustomMenu;
}
menu -parent $gMainWindow -label "Grammarizer" myCustomMenu;
menuItem -label "Open Grammarizer Window" -command "showMyWindow();";

showMyWindow();
